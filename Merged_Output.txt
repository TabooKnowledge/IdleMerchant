
store_state(self)
alarm[0] = 60;
clock = new Clock();
merchant = new Merchant();
travel = new Travel();
diary = new Diary();
scenery = new Scenery();
components = {clock:clock, merchant:merchant, travel:travel, diary:diary, scenery:scenery};
restore_state(components);

alarm[0] = 60;

scenery.draw(clock);
var delta = clock.tick(travel.pace_multiplier());
travel.update(delta);
merchant.exist(clock);
diary.update(clock);
scenery.update(clock);
print(diary.ledger)



function Travel() constructor {
	fatigue = 0;
	fatigue_rate = 0.0001;
	recovery_rate = 0.2;
	
	update = function(distance_delta) {
		self.fatigue += distance_delta * self.fatigue_rate;
		print("Fatigue: " + string(self.fatigue));
		self.fatigue = clamp(self.fatigue, 0, 1);
		 if (self.fatigue >= 1) {
			 self.fatigue = 0;
			 return true;
		 };
		 return false;
	};
	
	pace_multiplier = function() {
		return lerp(1, 0.7, self.fatigue);
	};
};


function Clock() constructor {
	distance = 0;
	time = 0;
	last_time = date_current_datetime();
	pace = 20;
	offline_distance = 0;
	total_journey_length = 1000;
	journey_complete = false;
	
	catch_up = function() {
		var now = date_current_datetime();
		var delta_seconds = (now - self.last_time) * 86400;
		self.time += delta_seconds;
		self.offline_distance = delta_seconds * self.pace;
		self.distance += self.offline_distance;
		self.distance = clamp(self.distance, 0, self.total_journey_length);
		self.last_time = now;
	};
	
	tick = function(multiplier) {
		self.time += (delta_time / 1000000);
		
		var delta = self.pace * multiplier * (delta_time / 1000000);
		
		self.distance += delta;
		self.distance = clamp(self.distance, 0, self.total_journey_length);
		
		if (self.distance == self.total_journey_length) {
			self.journey_complete = true;
		};
		
		return delta;
	};
};

function Merchant() constructor {
	age = 0;
	
	exist = function(clock) {
		self.age = clock.time;
	};
};

function Scenery() constructor {
	max_span = 960;
	scroll = 0;
	red_start_x = room_width;
	blue_start_x = room_width*2;
	green_start_x = room_width*3;
	
	update = function(clock) {
		self.scroll = clock.distance mod self.max_span;
	};
	
	draw = function(clock) {
		var red_x = self.red_start_x - self.scroll;
		var blue_x = self.blue_start_x - self.scroll;
		var green_x = self.green_start_x - self.scroll;
		
		draw_sprite(spr_red_background, 0, red_x, 0);
		draw_sprite(spr_blue_background, 0, blue_x, 0);
		draw_sprite(spr_green_background, 0, green_x, 0);


		draw_text(25, 25, string_format(clock.distance, 0, 0));
		draw_text(25, 50, string_format(clock.offline_distance, 0, 0));
	};	
};


function Diary() constructor {
	next_event = 0;
    ledger = [];
	events = [
		[160, "Passed an odd looking lizard"],
		[320, "Met a friendly poet"],
		[480, "Rested under a large tree"],
		[960, "Reached the edge of the province"],
	];
	
	update = function(clock) {
		while (self.next_event < array_length(self.events) && clock.distance >= self.events[self.next_event][EVENTS.DISTANCE]) {
			array_push(self.ledger, self.events[self.next_event][EVENTS.EVENT]);
			self.next_event++;
		};
	};
};


function save(data, file_name) {
	var json_string = json_stringify(data);
	var file = file_text_open_write(file_name);
	file_text_write_string(file, json_string);
	file_text_close(file);
	return true;
};

function load(file_name) {
	if (!file_exists(file_name)) return undefined;
	    var file = file_text_open_read(file_name);
	    var json_string = "";
	    while (!file_text_eof(file)) json_string += file_text_readln(file);
	    file_text_close(file);

	    return json_parse(json_string);
};

function store_state(controller) {
	var data = {
	    distance: controller.clock.distance,
		time: controller.clock.time,
	    last_time: date_current_datetime(),
		next_event: controller.diary.next_event,
		ledger: controller.diary.ledger,
		fatigue: controller.travel.fatigue,
	};
	save(data, "save.json");
};

function restore_state(components) {
	var save_data = load("save.json");
	if (save_data != undefined){
		components.clock.distance = save_data.distance;
		components.clock.last_time = save_data.last_time;
		components.diary.next_event = save_data.next_event;
		components.diary.ledger = save_data.ledger;
		components.travel.fatigue = save_data.fatigue;
		components.clock.catch_up();
		components.diary.update(clock);
		//array_push(diary.ledger, "Away for " +string_format((clock.offline_distance / 60), 0, 0) + " minutes. " + "Merchant traveled " + string(clock.offline_distance) + " meters.");
		return true;
	};
	return false;
};

function print(_strng="") {
    if (_strng == "") return;
    show_debug_message(_strng);
}

enum EVENTS {
	DISTANCE = 0,
	EVENT = 1
}
